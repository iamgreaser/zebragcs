param bool @physblock true
param int @char 233
param int @fgcolor 15
param int @bgcolor 0

param int @score 0
global int $foodtoplace
param int @foodtoembed 5
param entity @follower noentity

local pos %oldpos thispos
local int %x 0
local int %y 0
local dir %movedir east
local dir %lastmovedir east
local int %nextcolor 15

on state main {
  set %oldpos thispos
  send %movedir eat self
  set %lastmovedir %movedir
  move %movedir else {
    set @physblock false
    goto dead
  }
  if @physblock {
    if [ge @foodtoembed 1] {
      if @follower {
        send @follower extend
      } else {
        set %nextcolor @fgcolor
        dec %nextcolor 1
        if [lt %nextcolor 9] {
          set %nextcolor 14
        }
        set @physblock false
        spawninto @follower %oldpos tail {
          set @leader self
          set @player self
          set @fgcolor %nextcolor
        }
        set @physblock true
      }
      dec @foodtoembed 1
    }
    sleep 2
  }
}
on state dead {
  set @char 32
  set @bgcolor 0
  sleep 1
  send @follower die
  goto waitdead
}
on state waitdead {
}
on state actuallydead {
  say "Game over! Final score: \(@score)"
  sleep 20
  die
}

on event actuallydie {
  goto actuallydead
}

on event atefood {
  inc @foodtoembed 3
  inc @score 1
  say "Score: [\(@score)]"
}

on event typeup { if [ne %lastmovedir south] { set %movedir north } }
on event typedown { if [ne %lastmovedir north] { set %movedir south } }
on event typeleft { if [ne %lastmovedir east] { set %movedir west } }
on event typeright { if[ ne %lastmovedir west] { set %movedir east } }
