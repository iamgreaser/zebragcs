# vim: sw=2 sts=2 et syntax= :

param int @fgcolor 15
param int @bgcolor 4
param int @char 2
param int @health 100
param int @ammo 2
param int @gems 0
param int @score 0
param bool @shiftpressed false
param dir @nextdir idle
global entity $touchsrc
global entity $damagesrc
global int $givecount
global pos $playerpos
global dir $pushdir
global dir $shotdir
global bool $didmsgoutofammo
global pos $teleportpos
local pos %prevpos thispos

on state main {
  if eq @nextdir idle {
    sleep 1
  } else {
    if @shiftpressed {
      if ge @ammo 1 {
        dec @ammo 1
        spawn @nextdir bullet {
          set @harmplayer false
          set @movedir @nextdir
        } else {
          set $shotdir @nextdir
          send @nextdir shot
          set $shotdir @nextdir
          send @nextdir playershot
        }
      } else {
        # TODO: Play sound --GM
        if not $didmsgoutofammo {
          set $didmsgoutofammo true
          say "You don't have any ammo!"
        }
      }
      sleep 1
    } else {
      set $pushdir @nextdir
      set %prevpos thispos
      set $touchsrc self
      send @nextdir touch
      if eq %prevpos thispos {
        move @nextdir
      }
    }
    set @nextdir idle
  }
  set $playerpos thispos
}

on state dead {
}

on event teleport {
  move $teleportpos
}

on event pressshift { set @shiftpressed true }
on event releaseshift { set @shiftpressed false }

on event typeup { set @nextdir north }
on event typedown { set @nextdir south }
on event typeleft { set @nextdir west }
on event typeright { set @nextdir east }

on event typew {
  set $pushdir n
  send $pushdir push
  spawn n ammo {
  }
}

on event enemydamage {
  dec @health $givecount
  send $damagesrc ackenemydamage
  if ge @health 1 {
    say "Ouch!"
  } else {
    say "Game over - Press ESCAPE"
    goto dead
  }
}

on event enemyshot {
  dec @health 10
  if ge @health 1 {
    say "Ouch!"
  } else {
    say "Game over - Press ESCAPE"
    goto dead
  }
}

on event giveammo { inc @ammo $givecount }
on event givegems { inc @gems $givecount }
on event givehealth { inc @health $givecount }
on event givescore { inc @score $givecount }
