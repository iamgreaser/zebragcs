# vim: sw=2 sts=2 et syntax= :

param int @fgcolor 15
param int @bgcolor 4
param int @char 2
param int @health 100
param int @ammo 2
param int @gems 0
param int @score 0
param bool @shiftpressed false
param dir @nextdir idle
param player @parentplayer noplayer
global entity $playerent
global bool $didmsgoutofammo
global bool $didmsgforest
local pos %prevpos thispos


local int %givecount 0
local entity %froment noentity
local entity %blameent noentity
local pos %teleportpos idle
local dir %pushdir idle
local dir %shotdir idle

on state main {
  if [haslayer hud] {
    lrectfill hud 0 0 8 2 [cell 32 15 1]
    set [layer hud 1 0] [cell 3 12 1]
    set [layer hud 1 1] [cell 132 3 1]
    lprintright hud 6 0 15 1 @health
    lprintright hud 6 1 15 1 @ammo
  }

  set $playerent self
  if [eq @nextdir idle] {
    sleep 1
  } else {
    if @shiftpressed {
      if [ge @ammo 1] {
        dec @ammo 1
        spawn @nextdir bullet {
          set @harmplayer false
          set @movedir @nextdir
          set @ownerentity self
        } else {
          send @nextdir playershot self @nextdir self
          send @nextdir shot self @nextdir self
        }
      } else {
        # TODO: Play sound --GM
        if [not $didmsgoutofammo] {
          set $didmsgoutofammo true
          say "You don't have any ammo!"
        }
      }
      sleep 1
    } else {
      set %prevpos thispos
      send @nextdir touch self @nextdir
      if [eq %prevpos thispos] {
        if [and [haslayer forest]
                [ne [layer forest @nextdir]
                    [cell 0 0 0]]] {
          set [layer forest @nextdir] [cell 0 0 0]
          # TODO: Play sound --GM
          if [not $didmsgforest] {
            set $didmsgforest true
            say "A path is cleared through the forest."
          }
        }
        move @nextdir
      }
    }
    set @nextdir idle
  }
}

on state dead {
  if [eq $playerent self] {
    set $playerent noentity
  }
  set @char 1
  sleep 1
}

on event disown {
  die
}

on event teleport %teleportpos {
  move %teleportpos
}

on event pressshift { set @shiftpressed true }
on event releaseshift { set @shiftpressed false }

on event typeup { set @nextdir north }
on event typedown { set @nextdir south }
on event typeleft { set @nextdir west }
on event typeright { set @nextdir east }

on event typew {
  set %pushdir n
  send %pushdir push %pushdir
  spawn n ammo {
  }
}

on event enemydamage %froment %givecount {
  dec @health %givecount
  send %froment ackenemydamage self
  if [ge @health 1] {
    say "Ouch!"
  } else {
    #say "Game over - Press ESCAPE"
    say "You died - Press R to respawn"
    send @parentplayer needrespawn
    set @parentplayer noplayer
    goto dead
  }
}

on event enemyshot %froment %shotdir %blameent {
  dec @health 10
  if [ge @health 1] {
    say "Ouch!"
  } else {
    #say "Game over - Press ESCAPE"
    say "You died - Press R to respawn"
    send @parentplayer needrespawn
    set @parentplayer noplayer
    goto dead
  }
}

on event giveammo %givecount { inc @ammo %givecount }
on event givegems %givecount { inc @gems %givecount }
on event givehealth %givecount { inc @health %givecount }
on event givescore %givecount { inc @score %givecount }
