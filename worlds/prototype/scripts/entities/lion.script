# vim: sw=2 sts=2 et syntax= :

param int @fgcolor 12
param int @char 234
param bool @physblock true

param int @intelligence 4

global entity $playerent

local entity %froment idle
local dir %pushdir idle


on state main {
  sleep 2
  if @physblock {
    set %pushdir randomdir
    if $playerent {
      if [le [random 0 9] @intelligence] {
        set %pushdir [seek $playerent]
      }
    }

    send %pushdir enemydamage self 10
    # The ack should kill this but just in case
    # (apparently it sometimes doesn't? I do want to fix that. --GM)
    if @physblock {
      send %pushdir push %pushdir
      move %pushdir
    } else {
      say "...wait, what?"
    }
  } else {
    goto dead
  }
}

on state dead {
  # TODO: Animate this --GM
  die
}

on event ackenemydamage %froment {
  if @physblock {
    set @physblock false
    goto dead
  }
}

on event push %pushdir {
  if @physblock {
    send %pushdir push %pushdir
    move %pushdir else { die }
  }
}

on event playershot {
  if @physblock {
    set @physblock false
    goto dead
  }
}

on event touch %froment %pushdir {
  if @physblock {
    set @physblock false
    send %froment enemydamage self 10
    goto dead
  }
}
