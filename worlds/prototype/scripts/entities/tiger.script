# vim: sw=2 sts=2 et syntax= :

param int @fgcolor 11
param int @char 227
param bool @physblock true

param int @intelligence 4
param int @firingrate 4

global entity $playerent

local dir %bulletdir idle
local int %playerdirx 0
local int %playerdiry 0
local int %playerdirax 0
local int %playerdiray 0
local pos %playerpos thispos

local entity %froment noentity
local entity %blameent noentity
local dir %pushdir idle
local dir %shotdir idle

on state main {
  sleep 2
  if @physblock {
    set %pushdir randomdir
    if $playerent {
      if [le [random 0 9] @intelligence] {
        set %pushdir [seek $playerent]
      }
    }

    send %pushdir enemydamage self 10
    # The ack should kill this but just in case
    # (apparently it sometimes doesn't? I do want to fix that. --GM)
    if @physblock {
      send %pushdir push %pushdir
      move %pushdir

      # Now attempt the shot
      if $playerent {
        if [le [random 0 26] @firingrate] {
          set %playerdirx [dirx %playerpos]
          set %playerdiry [diry %playerpos]
          set %playerdirax %playerdirx
          set %playerdiray %playerdiry
          if [lt %playerdirax 0] { mul %playerdirax -1 }
          if [lt %playerdiray 0] { mul %playerdiray -1 }

          set %bulletdir idle
          if [le %playerdirax 2] {
            if [gt %playerdiry 0] {
              set %bulletdir south
            } else {
              set %bulletdir north
            }
          } else {
            if [le %playerdiray 2] {
              if [gt %playerdirx 0] {
                set %bulletdir east
              } else {
                set %bulletdir west
              }
            }
          }

          if [ne %bulletdir idle] {
            spawn %bulletdir bullet {
              set @movedir %bulletdir
              set @harmenemy false
              set @ownerentity self
            }
          }
        }
      }
    } else {
      say "...wait, what?"
    }
  } else {
    goto dead
  }
}

on state dead {
  # TODO: Animate this --GM
  die
}

on event ackenemydamage %froment {
  if @physblock {
    set @physblock false
    goto dead
  }
}

on event push %pushdir {
  if @physblock {
    send %pushdir push %pushdir
    move %pushdir else { die }
  }
}

on event playershot %froment %shotdir %blameent {
  if @physblock {
    set @physblock false
    goto dead
  }
}

on event touch %froment %pushdir {
  if @physblock {
    set @physblock false
    send %froment enemydamage self 10
    goto dead
  }
}
