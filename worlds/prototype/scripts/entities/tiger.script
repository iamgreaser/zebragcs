# vim: sw=2 sts=2 et syntax= :

param int @fgcolor 11
param int @char 227
param bool @physblock true

param int @intelligence 4
param int @firingrate 4

global int $givecount
global dir $pushdir
global bool $didmsgammoget
global entity $playerent
global entity $touchsrc
global entity $damagesrc

local dir %bulletdir idle
local int %playerdirx 0
local int %playerdiry 0
local int %playerdirax 0
local int %playerdiray 0
local pos %playerpos thispos

on state main {
  sleep 2
  if @physblock {
    set $pushdir randomdir
    if $playerent {
      set %playerpos [posof $playerent]
      if [le [random 0 9] @intelligence] {
        set $pushdir [seek %playerpos]
      }
    }

    set $givecount 10
    set $damagesrc self
    send $pushdir enemydamage
    # The ack should kill this but just in case
    # (apparently it sometimes doesn't? I do want to fix that. --GM)
    if @physblock {
      send $pushdir push
      move $pushdir

      # Now attempt the shot
      if $playerent {
        if [le [random 0 26] @firingrate] {
          set %playerdirx [dirx %playerpos]
          set %playerdiry [diry %playerpos]
          set %playerdirax %playerdirx
          set %playerdiray %playerdiry
          if [lt %playerdirax 0] { mul %playerdirax -1 }
          if [lt %playerdiray 0] { mul %playerdiray -1 }

          set %bulletdir idle
          if [le %playerdirax 2] {
            if [gt %playerdiry 0] {
              set %bulletdir south
            } else {
              set %bulletdir north
            }
          } else {
            if [le %playerdiray 2] {
              if [gt %playerdirx 0] {
                set %bulletdir east
              } else {
                set %bulletdir west
              }
            }
          }

          if [ne %bulletdir idle] {
            spawn %bulletdir bullet {
              set @movedir %bulletdir
              set @harmenemy false
            }
          }
        }
      }
    } else {
      say "...wait, what?"
    }
  } else {
    goto dead
  }
}

on state dead {
  # TODO: Animate this --GM
  die
}

on event ackenemydamage {
  if @physblock {
    set @physblock false
    goto dead
  }
}

on event push {
  if @physblock {
    send $pushdir push
    move $pushdir else { die }
  }
}

on event playershot {
  if @physblock {
    set @physblock false
    goto dead
  }
}

on event touch {
  if @physblock {
    set @physblock false
    set $givecount 10
    set $damagesrc self
    send $touchsrc enemydamage
    goto dead
  }
}
